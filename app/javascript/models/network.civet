import ApplicationRecord from "./application_record.civet"
import Ipv4Pool from "./ipv4_pool.civet"
import Ipv6Pool from "./ipv6_pool.civet"
import ipaddr from "ipaddr.js"

namespace Network
  export interface InitParams extends ApplicationRecord.InitParams
    name: string
    vlan: number
    auth: boolean
    locked: boolean
    specific: boolean
    global: boolean
    dhcp: boolean
    ra: "router" | "unmanaged" | "managed" | "assist" | "stateless" | "disabled"
    note: string
    ipv4_network_address: string?
    ipv4_prefix_length: number
    ipv4_gateway_address: string?
    ipv6_network_address: string?
    ipv6_prefix_length: number
    ipv6_gateway_address: string?
    ipv4_pools: Ipv4Pool.InitParams[]
    ipv6_pools: Ipv6Pool.InitParams[]
    current_user: Record<"auth" | "usable" | "manageable", boolean>

class Network extends ApplicationRecord
  @networks: Map<string, Network?> := new Map

  @fetch(id: string | number | bigint): Promise<Network?>
    id = String(id) if id !<? "string"

    unless /^\d+$/.test(id)
      throw new TypeError(`${id} is not positive integer nmuber string`)

    return @networks.get(id) if @networks.has(id)

    url := `/networks/${id}.json`
    response := await fetch(url)

    if response.status is 404
      console.warn `network ${id} is not found for current user`
      @networks.set(id, undefined)
      return

    data := await response.json()
    network := new Network(data)
    @networks.set(id, network)
    network

  readonly name: string
  readonly vlan: number
  readonly auth: boolean
  readonly locked: boolean
  readonly specific: boolean
  readonly global: boolean
  readonly dhcp: boolean
  readonly ra: "router" | "unmanaged" | "managed" | "assist" | "stateless" | "disabled"
  readonly note: string
  readonly current_user: Record<"auth" | "usable" | "manageable", boolean>
  readonly ipv4_network: ipaddr.IPv4?
  readonly ipv4_prefix_length: number
  readonly ipv4_gateway: ipaddr.IPv4?
  readonly ipv6_network: ipaddr.IPv6?
  readonly ipv6_prefix_length: number
  readonly ipv6_gateway: ipaddr.IPv6?
  readonly ipv4_pools: Ipv4Pool[]
  readonly ipv6_pools: Ipv6Pool[]
  readonly ipv4_config_list: string[]
  readonly ipv6_config_list: string[]

  readonly ipv4: {
    network: ipaddr.IPv4?
    prefix_length: number
    gateway: ipaddr.IPv4?
    pools: Ipv4Pool[]
    config_list: string[]
    dhcp: boolean
  }
  readonly ipv6: {
    network: ipaddr.IPv6?
    prefix_length: number
    gateway: ipaddr.IPv6?
    pools: Ipv6Pool[]
    config_list: string[]
    dhcp: boolean
    slaac: boolean
  }

  @({@name, @vlan, @auth, @locked, @specific, @global, @dhcp, @ra, @note,
      ipv4_network_address, @ipv4_prefix_length, ipv4_gateway_address,
      ipv6_network_address, @ipv6_prefix_length, ipv6_gateway_address,
      ipv4_pools, ipv6_pools,
      @current_user, ...props}: Network.InitParams)
    super(props)

    if ipv4_network_address
      @ipv4_network = ipaddr.IPv4.parse(ipv4_network_address)
    if ipv4_gateway_address
      @ipv4_gateway = ipaddr.IPv4.parse(ipv4_gateway_address)
    if ipv6_network_address
      @ipv6_network = ipaddr.IPv6.parse(ipv6_network_address)
    if ipv6_gateway_address
      @ipv6_gateway = ipaddr.IPv6.parse(ipv6_gateway_address)

    @ipv4_pools = ipv4_pools.map (pool) -> new Ipv4Pool(pool)
    @ipv6_pools = ipv6_pools.map (pool) -> new Ipv6Pool(pool)

    @ipv4_config_list = @availableIpConfig(@ipv4_network, @ipv4_pools)
    slaac := @ra is in ["unmanaged", "assist", "stateless"]
    @ipv6_config_list = @availableIpConfig(@ipv6_network, @ipv6_pools, slaac)

    @ipv4 = {
      network: @ipv4_network
      prefix_length: @ipv4_prefix_length
      gateway: @ipv4_gateway
      pools: @ipv4_pools
      config_list: @ipv4_config_list
      dhcp: @dhcp
    }

    @ipv6 = {
      network: @ipv6_network
      prefix_length: @ipv6_prefix_length
      gateway: @ipv6_gateway
      pools: @ipv6_pools
      config_list: @ipv6_config_list
      dhcp: @ra is in ["managed", "assist", "stateless"]
      slaac: slaac
    }

    console.debug @ra
    console.debug @ipv6

  availableIpConfig(ip: ipaddr.IPv4 | ipaddr.IPv6 | undefined,
    pools: Ipv4Pool[] | Ipv6Pool[], slaac: boolean = false): string[]
    return [] unless @current_user.usable
    return ["disabled"] unless ip?

    configs := new Set(["disabled"])
    configs.add(pool.ip_config) for each pool of pools
    configs.add("dynamic") if slaac
    configs.add("manual") if @current_user.manageable
    [...configs]

  has(ip: "ipv4" | "ipv6"): boolean
    @[ip].network?

export default Network
